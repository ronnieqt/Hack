// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/12/Sys.jack

/**
 * A library that supports various program execution services.
 */
class Sys {

    /** Performs all the initializations required by the OS. */
    function void init() {
        do Memory.init();
        do Math.init();
        do Screen.init();
        do Output.init();
        do Keyboard.init();
        do Main.main();
        do Sys.halt();
        return;
    }

    /** Halts the program execution. */
    function void halt() {
        // enters an infinite loop
        while (true) {}
        return;
    }

    /** Waits approximately duration milliseconds and returns.  */
    function void wait(int duration) {
        var int delay;
        // duration must be positive
        if (~(duration > 0)) {
            do Sys.error(1);
        }
        while (duration > 0) {
            let delay = 50;
            while (delay > 0) {
                let delay = delay - 1;
            }
            let duration = duration - 1;
        }
        return;
    }

    /** Displays the given error code in the form "ERR<errorCode>",
     *  and halts the program's execution. */
    function void error(int errorCode) {
        do Output.printChar(69);  // E
        do Output.printChar(82);  // R
        do Output.printChar(82);  // R
        do Output.printInt(errorCode);
        do Sys.halt();
        return;
    }

}